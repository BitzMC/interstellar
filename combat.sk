# When players join, make sure the variablrs are set.
on join:
	set {mana.%player%} to 0
	set {maxmana.%player%} to 100
	if {class.%player%} is not set:
		set {class.%player%} to 0
		set {classname.%player%} to "&c&lNone"

# Every 10 ticks (0.5 seconds) update the action bar.
every 10 ticks:
	loop all players:
		send action bar "&b%{mana.%loop-player%}%&b/%{maxmana.%loop-player%}% Mana&r&7 |&r %{classname.%loop-player%}%" to loop-player

# Every 100 ticks (5 seconds) every player gets 1 more mana if not full.
every 100 ticks:
	loop all players:
		if {mana.%loop-player%} is greater than {maxmana.%loop-player%} - 1:
			set {mana.%loop-player%} to {maxmana.%loop-player%}
			stop
		else:
			add 1 to {mana.%loop-player%}

# Resets mana back to 0.
command /manareset:
	trigger:
		message "&6Mana has been reset!"
		set {mana.%player%} to 0
	
# Fills mana to the maximum defined mana.
command /manamax:
	trigger:
		# If the player does not have the permission, they can't run it.
		if player does not have permission "bitzcombat.fillmana":
			message "&cYou do not have permission to run this command."
		else:
			message "&6Mana has been maxed out!"
			set {mana.%player%} to {maxmana.%player%}

# The GUI for the power sets.
command /powerselect:
	trigger:
		set metadata tag "power-select-gui" of player to chest inventory with 3 rows named "&6Power Select"
		set slot 10 of metadata tag "power-select-gui" of player to magma block named "&6&lX5-07" with lore "%nl%&7Used in causing massive amounts of%nl%&7damage, at the cost of your own life.%nl%%nl%&fRequires %{maxmana.%player%}% Mana!%nl%&aClick&f to join."
		set slot 12 of metadata tag "power-select-gui" of player to obsidian named "&d&lX2-08" with lore "%nl%&7Used in causing high amounts of damage.%nl%%nl%&fRequires %{maxmana.%player%}% Mana!%nl%&aClick&f to join."
		set slot 14 of metadata tag "power-select-gui" of player to iron block named "&7&lX4-05" with lore "%nl%&7Used in keeping you and your team alive.%nl%%nl%&fRequires %{maxmana.%player%}% Mana!%nl%&aClick&f to join."
		set slot 16 of metadata tag "power-select-gui" of player to ice named "&b&lX5-04" with lore "%nl%&7Used in guaranteeing you, and your team stay alive,%nl%&7at the cost of not doing much damage.%nl%%nl%&6NOTICE:&r&7 This class is recommended for%nl%&7experienced players, you may find it too%nl%&7difficult to time correctly.%nl%%nl%&fRequires %{maxmana.%player%}% Mana!%nl%&aClick&f to join."
		open (metadata tag "power-select-gui" of player) to player

# For checking GUI events.
on inventory click:
	# Check if the clicked inventory is the one we just created.
	if event-inventory = (metadata tag "power-select-gui" of player):
		# Cancel the clicking. This makes the Item unstealable.
		cancel event
		# Make sure the GUI closes when selecting a set.
		close player's inventory
		# Check which slot is being clicked.
		if index of event-slot is 10:
			if {mana.%player%} is equal to {maxmana.%player%}:
				set {class.%player%} to 1
				set {classname.%player%} to "&6&lX5-07"
				message "&fYou are now using &6&lX5-07&r&f!"
				set {mana.%player%} to 0
			else:
				message "&cYou must have %{maxmana.%player%} - {mana.%player%}% more mana to use this."
		else if index of event-slot is 12:
			if {mana.%player%} is equal to {maxmana.%player%}:
				set {class.%player%} to 2
				set {classname.%player%} to "&d&lX2-08"
				message "&fYou are now using &d&lX2-08&r&f!"
				set {mana.%player%} to 0
			else:
				message "&cYou must have %{maxmana.%player%} - {mana.%player%}% more mana to use this."
		else if index of event-slot is 14:
			if {mana.%player%} is equal to {maxmana.%player%}:
				set {class.%player%} to 3
				set {classname.%player%} to "&7&lX4-05"
				message "&fYou are now using &7&lX4-05&r&f!"
				set {mana.%player%} to 0
			else:
				message "&cYou must have %{maxmana.%player%} - {mana.%player%}% more mana to use this."
		else if index of event-slot is 16:
			if {mana.%player%} is equal to {maxmana.%player%}:
				set {class.%player%} to 4
				set {classname.%player%} to "&b&lX5-04"
				message "&fYou are now using &b&lX5-04&r&f!"
				set {mana.%player%} to 0
			else:
				message "&cYou must have %{maxmana.%player%} - {mana.%player%}% more mana to use this."

# When you die, and mana is at 100 or higher, revive the player (primary ability)
on death of player:
	if {mana.%player%} is greater than 99:
		# Sci-fi sound effects.
		play sound "block.beacon.power_select" with volume 2 to the player
		add -100 to {mana.%player%}
		cancel event
		set player's health to 0.0001
		set player's hunger to 2
		apply potion of resistance of tier 20 to player for 12 seconds
		apply potion of night vision of tier 1 to player for 3 seconds
		apply potion of blindness of tier 1 to player for 1.5 seconds
		apply potion of glowing of tier 1 to player for 3 seconds
		message "&cReviving..."
		loop 60 times:
			# Move the player up continuously upwards.
			set velocity of player to new Vector 0, 0.1, 0
			play cloud at the player
			wait a tick
		loop 3 times:
			# And slam the player back on to the ground
			set velocity of player to new Vector 0, -1.65, 0
			play mobspawner flames at the player
			wait a tick
		# Make fake lightning that doesn't do anything.
		strike fake fake lightning at player
		play sound "ambient.weather.thunder" with volume 2 to the player
		apply potion of fire resistance of tier 1 to player for 30 seconds
		send title "" with subtitle "&cYou have been revived!" to player for 2 seconds with fadein 0.5 second and fade out 0.5 second
		#create a safe explosion of force 3 at the player
		loop 160 times:
			play cloud at the player
			wait a tick

# If the player has 35 mana or higher, give a small window of invincibility when a sword is
# right-clicked while crouching.
on right click with sword:
	player is sneaking
	if {mana.%player%} is greater than 34:
		play sound "block.beacon.power_select" with volume 2 to the player
		add -35 to {mana.%player%}
		remove absorption from player
		remove regeneration from player
		apply potion of absorption of tier 1 to player for 8 seconds
		apply potion of regeneration of tier 1 to player for 8 seconds
		apply potion of resistance of tier 20 to player for 1 seconds

# Reset players mana on respawn.
on respawn:
	set {mana.%player%} to 0
    
# When you ear, gain 2 mana.
on eat:
	if {mana.%player%} is less than {maxmana.%player%}:
		add 2 to {mana.%player%}
